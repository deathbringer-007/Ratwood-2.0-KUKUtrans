//Fluff structures serve no purpose and exist only for enriching the environment. They can be destroyed with a wrench.

/obj/structure/fluff
	name = "装饰结构"
	desc = ""
	icon_state = "minibar"
	anchored = TRUE
	density = FALSE
	opacity = 0
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 150
	var/deconstructible = TRUE

/obj/structure/fluff/pillow
	name = "靠枕"
	desc = "柔软蓬松的靠枕。把脑袋枕上去时，总让人无比放松。"
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "pillow"
	density = FALSE

/obj/structure/fluff/pillow/red
	color = CLOTHING_RED

/obj/structure/fluff/pillow/blue
	color = CLOTHING_BLUE

/obj/structure/fluff/pillow/green
	color = CLOTHING_DARK_GREEN

/obj/structure/fluff/pillow/brown
	color = CLOTHING_BROWN

/obj/structure/fluff/pillow/magenta
	color = CLOTHING_MAGENTA

/obj/structure/fluff/pillow/purple
	color = CLOTHING_PURPLE

/obj/structure/fluff/pillow/black
	color = CLOTHING_BLACK

/obj/structure/fluff/drake_statue //Ash drake status spawn on either side of the necropolis gate in lavaland.
	name = "龙兽雕像"
	desc = "这大概会是你此生唯一一次能近距离看见它的模样，还能活着讲出来。"
	icon = 'icons/effects/64x64.dmi'
	icon_state = "drake_statue"
	pixel_x = -16
	density = TRUE
	deconstructible = FALSE
	layer = EDGED_TURF_LAYER

/obj/structure/fluff/drake_statue/falling //A variety of statue in disrepair; parts are broken off and a gemstone is missing
	desc = ""
	icon_state = "drake_statue_falling"

/obj/structure/fluff/paper/corner
	icon_state = "papercorner"

/obj/structure/fluff/paper/stack
	name = "厚厚一摞纸张"
	desc = "你已经能感觉到双眼开始发直，乏味也正一点点爬上心头。"
	icon_state = "paperstack"

/obj/structure/fluff/divine
	name = "奇迹"
	icon = 'icons/obj/hand_of_god_structures.dmi'
	anchored = TRUE
	density = TRUE

/obj/structure/fluff/divine/nexus
	name = "中枢"
	desc = ""
	icon_state = "nexus"

/obj/structure/fluff/divine/conduit
	name = "导流柱"
	desc = ""
	icon_state = "conduit"

/obj/structure/fluff/divine/convertaltar
	name = "转化祭坛"
	desc = ""
	icon_state = "convertaltar"
	density = FALSE
	can_buckle = 1

/obj/structure/fluff/divine/powerpylon
	name = "能量塔柱"
	desc = ""
	icon_state = "powerpylon"
	can_buckle = 1

/obj/structure/fluff/divine/defensepylon
	name = "防御塔柱"
	desc = ""
	icon_state = "defensepylon"

/obj/structure/fluff/divine/shrine
	name = "神龛"
	desc = ""
	icon_state = "shrine"

/obj/structure/fluff/big_chain
	name = "巨链"
	desc = ""
	icon = 'icons/effects/32x96.dmi'
	icon_state = "chain"
	layer = ABOVE_OBJ_LAYER
	anchored = TRUE
	density = TRUE
	deconstructible = FALSE

/obj/structure/fluff/railing
	name = "栏杆"
	desc = "一道简易木制护栏，用来防止失足坠落。"
	icon = 'icons/obj/railing.dmi'
	icon_state = "railing"
	density = FALSE
	anchored = TRUE
	deconstructible = FALSE
	flags_1 = ON_BORDER_1
	climbable = TRUE
	layer = ABOVE_MOB_LAYER
	/// Living mobs can lay down to go past
	var/pass_crawl = TRUE
	/// Projectiles can go past
	var/pass_projectile = TRUE
	/// Throwing atoms can go past
	var/pass_throwing = TRUE
	/// Throwing/Flying non mobs can always exit the turf regardless of other flags
	var/allow_flying_outwards = TRUE

/obj/structure/fluff/railing/Initialize(mapload)
	. = ..()
	init_connect_loc_element()
	var/lay = getwlayer(dir)
	if(lay)
		layer = lay

/obj/structure/fluff/railing/proc/init_connect_loc_element()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/railing/proc/getwlayer(dirin)
	switch(dirin)
		if(NORTH)
			layer = BELOW_MOB_LAYER-0.01
		if(WEST)
			layer = BELOW_MOB_LAYER
		if(EAST)
			layer = BELOW_MOB_LAYER
		if(SOUTH)
			layer = ABOVE_MOB_LAYER
			plane = GAME_PLANE_UPPER

/obj/structure/fluff/railing/CanPass(atom/movable/mover, turf/target)
//	if(istype(mover) && (mover.pass_flags & PASSTABLE))
//		return 1
	if(istype(mover, /obj/projectile))
		return 1
	if(mover.throwing)
		return 1
	if(isobserver(mover))
		return 1
	if(isliving(mover))
		var/mob/living/M = mover
		if(M.movement_type & FLYING)
			return 1
		if(!(M.mobility_flags & MOBILITY_STAND))
			if(pass_crawl)
				return TRUE
	if(icon_state == "woodrailing" && (dir in CORNERDIRS))
		var/list/baddirs = list()
		switch(dir)
			if(SOUTHEAST)
				baddirs = list(SOUTHEAST, SOUTH, EAST)
			if(SOUTHWEST)
				baddirs = list(SOUTHWEST, SOUTH, WEST)
			if(NORTHEAST)
				baddirs = list(NORTHEAST, NORTH, EAST)
			if(NORTHWEST)
				baddirs = list(NORTHWEST, NORTH, WEST)
		if(get_dir(loc, target) in baddirs)
			return 0
	else if(get_dir(loc, target) == dir)
		return 0
	return 1

/obj/structure/fluff/railing/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER

	if(dir in CORNERDIRS)
		return

	if(isobserver(leaving))
		return

	if(get_dir(leaving.loc, new_location) != dir)
		return

	if(pass_projectile && istype(leaving, /obj/projectile))
		return

	if(pass_throwing && leaving.throwing)
		return

	if(pass_crawl && isliving(leaving))
		var/mob/living/M = leaving
		if(!(M.mobility_flags & MOBILITY_STAND))
			return

	leaving.Bump(src)
	return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/railing/OnCrafted(dirin)
	. = ..()
	var/lay = getwlayer(dir)
	if(lay)
		layer = lay

/obj/structure/fluff/railing/border/north
	dir = 1

/obj/structure/fluff/railing/border/east
	dir = 4

/obj/structure/fluff/railing/border/west
	dir = 8

/obj/structure/fluff/railing/corner
	icon_state = "border"
	density = FALSE
	dir = 9

/obj/structure/fluff/railing/corner/init_connect_loc_element()
	return

/obj/structure/fluff/railing/corner/north_east
	dir = 5

/obj/structure/fluff/railing/corner/south_west
	dir = 10

/obj/structure/fluff/railing/corner/south_east
	dir = 6

/obj/structure/fluff/railing/wood
	icon_state = "woodrailing"
	blade_dulling = DULLING_BASHCHOP
	layer = ABOVE_MOB_LAYER

/obj/structure/fluff/railing/wood/north
	dir = 1

/obj/structure/fluff/railing/wood/east
	dir = 4

/obj/structure/fluff/railing/wood/west
	dir = 8

/obj/structure/fluff/railing/stonehedge
	icon_state = "stonehedge"
	blade_dulling = DULLING_BASHCHOP
	layer = ABOVE_MOB_LAYER

/obj/structure/fluff/railing/border
	name = "护栏"
	desc = ""
	icon_state = "border"
	pass_crawl = FALSE

/obj/structure/fluff/railing/fence
	name = "木栅"
	desc = "一道简陋的屏障，或许还能把怪物挡在外头。"
	icon = 'icons/roguetown/misc/structure.dmi'
	attacked_sound = 'sound/misc/woodhit.ogg'
	destroy_sound = 'sound/misc/treefall.ogg'
	icon_state = "fence"
	density = TRUE
	opacity = TRUE
	anchored = TRUE
	layer = 2.91
	climbable = FALSE
	max_integrity = 400
	pass_crawl = FALSE
	climb_offset = 6

/obj/structure/fluff/railing/fence/Initialize(mapload)
	. = ..()
	smooth_fences()

/obj/structure/fluff/railing/fence/Destroy()
	..()
	smooth_fences()

/obj/structure/fluff/railing/fence/OnCrafted(dirin)
	. = ..()
	smooth_fences()

/obj/structure/fluff/railing/fence/proc/smooth_fences(neighbors)
	cut_overlays()
	if((dir == WEST) || (dir == EAST))
		var/turf/T = get_step(src, NORTH)
		if(T)
			for(var/obj/structure/fluff/railing/fence/F in T)
				if(F.dir == dir)
					if(!neighbors)
						F.smooth_fences(TRUE)
					var/mutable_appearance/MA = mutable_appearance(icon,"fence_smooth_above")
					MA.dir = dir
					add_overlay(MA)
		T = get_step(src, SOUTH)
		if(T)
			for(var/obj/structure/fluff/railing/fence/F in T)
				if(F.dir == dir)
					if(!neighbors)
						F.smooth_fences(TRUE)
					var/mutable_appearance/MA = mutable_appearance(icon,"fence_smooth_below")
					MA.dir = dir
					add_overlay(MA)

/obj/structure/fluff/railing/fence/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, target) == dir)
		return 0
	return 1

/obj/structure/fluff/railing/fence/flimsy
	name = "脆弱木栅"
	desc = "一道简陋的屏障，或许还能把怪物挡在外头。这一道看起来已经老旧风化，而且搭得相当仓促。"
	max_integrity = 180
	color = "#cccac5"

/obj/structure/bars
	name = "铁栏杆"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "bars"
	density = TRUE
	anchored = TRUE
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 700
	damage_deflection = 12
	integrity_failure = 0.15
	dir = SOUTH
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")

/obj/structure/bars/obj_break(damage_flag)
	loud_message("令人牙酸的金属刮擦断裂声骤然响起", hearing_distance = 14)
	. = ..()

/obj/structure/bars/CanPass(atom/movable/mover, turf/target)
	if(isobserver(mover))
		return 1
	if(istype(mover) && (mover.pass_flags & PASSGRILLE))
		return 1
	if(mover.throwing && !ismob(mover))
		return prob(66)
	return ..()

/obj/structure/bars/shop
	icon_state = "barsbent"
	layer = BELOW_OBJ_LAYER

/obj/structure/bars/rusty
	name = "锈蚀铁栏"
	desc = "这些栏杆看起来很脆弱。"
	color ="#ffcd9f"
	max_integrity = 200

/obj/structure/bars/shop/bronze
	color = "#ff9c1a"

/obj/structure/bars/chainlink
	icon_state = "chainlink"

/obj/structure/bars/steel
	name = "钢铁栏杆"
	max_integrity = 2000

/obj/structure/bars/tough
	max_integrity = 9000
	damage_deflection = 40

/obj/structure/bars/nopassthrow
	desc = "栏杆太过粗厚，任何东西都别想从缝隙里穿过去。"

/obj/structure/bars/nopassthrow/CanPass(atom/movable/mover, turf/target)
	return isobserver(mover)

/*
/obj/structure/bars/CheckExit(atom/movable/O, turf/target)
	if(istype(O) && (O.pass_flags & PASSGRILLE))
		return 1
	if(O.throwing && !ismob(O))
		return 1
	return !density
	..()
*/
/obj/structure/bars/obj_break(damage_flag)
	icon_state = "[initial(icon_state)]b"
	density = FALSE
	..()

/obj/structure/bars/cemetery
	icon_state = "cemetery"

/obj/structure/bars/passage
	icon_state = "passage0"
	density = TRUE
	max_integrity = 1500
	redstone_structure = TRUE

/obj/structure/bars/passage/steel
	name = "钢铁栏杆"
	max_integrity = 2000

/obj/structure/bars/passage/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "passage1"
		density = FALSE
	else
		icon_state = "passage0"
		density = TRUE

/obj/structure/bars/passage/shutter
	icon_state = "shutter0"
	density = TRUE
	opacity = TRUE
	redstone_structure = TRUE

/obj/structure/bars/passage/shutter/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "shutter1"
		density = FALSE
		opacity = FALSE
	else
		icon_state = "shutter0"
		density = TRUE
		opacity = TRUE

/obj/structure/bars/passage/shutter/open
	icon_state = "shutter1"
	density = FALSE
	opacity = FALSE

/obj/structure/bars/passage/shutter/hidden/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "shutter1"
		density = FALSE
		opacity = FALSE
		alpha = 60
	else
		icon_state = "shutter0"
		density = TRUE
		opacity = TRUE
		alpha = 255

/obj/structure/bars/passage/attackby(obj/item/I, mob/user, params)
	. = ..()
	var/obj/item = user.get_active_held_item()
	if(user.used_intent.type == /datum/intent/chisel )
		if (user.get_skill_level(/datum/skill/craft/engineering) <= 3)
			to_chat(user, span_warning("我还得更熟练些，才能在这条通道上刻下名字。"))
			return
		playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		user.visible_message("<span class='info'>[user]在这条通道上刻下了名字。</span>")
		if(do_after(user, 10))
			var/passagename
			passagename = input("你想在这条通道上刻下什么名字？")
			if (passagename)
				name = passagename + "(passage)"
				desc = "一条刻上了名字的通道"
			else
				name = "通道"
				desc = "一条刻痕被刮去的通道"
			playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		return
	else if(istype(item, /obj/item/rogueweapon/chisel/assembly))
		to_chat(user, span_warning("我要双手都腾出来，才能给这条通道重新刻名。"))

/obj/structure/bars/grille
	name = "格栅"
	desc = ""
	icon_state = "floorgrille"
	density = FALSE
	layer = TABLE_LAYER
	plane = GAME_PLANE_LOWER
	damage_deflection = 5
	blade_dulling = DULLING_BASHCHOP
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN | BLOCK_Z_IN_UP
	attacked_sound = list('sound/combat/hits/onmetal/grille (1).ogg', 'sound/combat/hits/onmetal/grille (2).ogg', 'sound/combat/hits/onmetal/grille (3).ogg')
	var/togg = FALSE
	redstone_structure = TRUE

/obj/structure/bars/grille/Initialize(mapload)
	AddComponent(/datum/component/squeak, list('sound/foley/footsteps/FTMET_A1.ogg','sound/foley/footsteps/FTMET_A2.ogg','sound/foley/footsteps/FTMET_A3.ogg','sound/foley/footsteps/FTMET_A4.ogg'), 40)
	dir = pick(GLOB.cardinals)
	return ..()

/obj/structure/bars/grille/obj_break(damage_flag)
	obj_flags = CAN_BE_HIT
	..()

/obj/structure/bars/grille/redstone_triggered()
	if(obj_broken)
		return
	testing("togge")
	togg = !togg
	playsound(src, 'sound/foley/trap_arm.ogg', 100)
	if(togg)
		testing("togge1")
		icon_state = "floorgrilleopen"
		obj_flags = CAN_BE_HIT
		var/turf/T = loc
		if(istype(T))
			for(var/mob/living/M in loc)
				T.Entered(M)
	else
		testing("togge2")
		icon_state = "floorgrille"
		obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN | BLOCK_Z_IN_UP

/obj/structure/bars/grille/attackby(obj/item/I, mob/user, params)
	. = ..()
	var/obj/item = user.get_active_held_item()
	if(user.used_intent.type == /datum/intent/chisel )
		if (user.get_skill_level(/datum/skill/craft/engineering) <= 3)
			to_chat(user, span_warning("我还得更熟练些，才能在这面格栅上刻下名字。"))
			return
		playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		user.visible_message("<span class='info'>[user]在这面格栅上刻下了名字。</span>")
		if(do_after(user, 10))
			var/grillename
			grillename = input("你想在这面格栅上刻下什么名字？")
			if (grillename)
				name = grillename + "(grille)"
				desc = "一面刻上了名字的格栅"
			else
				name = "格栅"
				desc = "一面刻痕被刮去的格栅"
			playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		return
	else if(istype(item, /obj/item/rogueweapon/chisel/assembly))
		to_chat(user, span_warning("我要双手都腾出来，才能给这面格栅重新刻名。"))

/obj/structure/bars/grille/rusty
	name = "生锈格栅"
	desc = "只要狠狠干上几下，应该就能把它砸开。"
	max_integrity = 70
	color = "#d9c8c1"

/obj/structure/bars/pipe
	name = "青铜管道"
	desc = ""
	icon_state = "pipe"
	density = FALSE
	layer = TABLE_LAYER
	plane = GAME_PLANE_LOWER
	damage_deflection = 5
	blade_dulling = DULLING_BASHCHOP
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN | BLOCK_Z_IN_UP
	attacked_sound = list('sound/combat/hits/onmetal/grille (1).ogg', 'sound/combat/hits/onmetal/grille (2).ogg', 'sound/combat/hits/onmetal/grille (3).ogg')
	var/togg = FALSE
	smeltresult = /obj/item/ingot/bronze

//===========================

/obj/structure/fluff/clock
	name = "挂钟"
	desc = ""
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	icon_state = "clock"
	density = FALSE
	anchored = FALSE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	dir = SOUTH
	break_sound = "glassbreak"
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = 'sound/combat/hits/onglass/glasshit.ogg'
	var/datum/looping_sound/clockloop/soundloop
	drag_slowdown = 3
	metalizer_result = /obj/item/roguegear/bronze

/obj/structure/fluff/clock/Initialize(mapload)
	soundloop = new(src, FALSE)
	soundloop.start()
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/clock/Destroy()
	if(soundloop)
		soundloop.stop()
	..()

/obj/structure/fluff/clock/obj_break(damage_flag)
	icon_state = "b[initial(icon_state)]"
	if(soundloop)
		soundloop.stop()
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	..()

/obj/structure/fluff/clock/attack_right(mob/user)
	handle_special_items_retrieval(user, src)

/obj/structure/fluff/clock/examine(mob/user)
	. = ..()
	if(obj_broken)
		return
	var/day = "……等等，今天到底是星期几？"
	switch(GLOB.dayspassed)
		if(1)
			day = "月日。"
		if(2)
			day = "提乌日。"
		if(3)
			day = "婚誓日。"
		if(4)
			day = "图勒日。"
		if(5)
			day = "芙蕾雅日。"
		if(6)
			day = "萨图恩日。"
		if(7)
			day = "太阳日。"
	. += "噢，不妙，现在是[day]的[station_time_timestamp("hh:mm")]。"
//		if(SSshuttle.emergency.mode == SHUTTLE_DOCKED)
//			if(SSshuttle.emergency.timeLeft() < 30 MINUTES)
//				. += span_warning("The last boat will leave in [round(SSshuttle.emergency.timeLeft()/600)] minutes.")

/obj/structure/fluff/clock/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/clock/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, mover) == dir)
		return 0
	return 1

/obj/structure/fluff/clock/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(get_dir(leaving.loc, new_location) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/wallclock
	name = "挂钟"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "wallclock"
	density = FALSE
	anchored = TRUE
	layer = BELOW_MOB_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	var/datum/looping_sound/clockloop/soundloop
	break_sound = "glassbreak"
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = 'sound/combat/hits/onglass/glasshit.ogg'
	pixel_y = 32
	metalizer_result = /obj/item/roguegear/bronze

/obj/structure/fluff/wallclock/attack_right(mob/user)
	if(user.mind && isliving(user))
		var/area/rogue/user_area = get_area(user)
		if(user_area?.no_special_item_retrieval) //area does not allow fetching special items, return
			return
		if(user.mind.special_items && user.mind.special_items.len)
			var/item = input(user, "我要取出什么？", "储物藏匿处") as null|anything in user.mind.special_items
			if(item)
				if(user.Adjacent(src))
					if(user.mind.special_items[item])
						var/path2item = user.mind.special_items[item]
						user.mind.special_items -= item
						var/obj/item/I = new path2item(user.loc)
						user.put_in_hands(I)
			return

/obj/structure/fluff/wallclock/Destroy()
	if(soundloop)
		soundloop.stop()
	..()

/obj/structure/fluff/wallclock/examine(mob/user)
	. = ..()
	if(obj_broken)
		return
	var/day = "……等等，今天到底是什么日子？"
	switch(GLOB.dayspassed)
		if(1)
			day = "月之日。"
		if(2)
			day = "提乌之日。"
		if(3)
			day = "婚誓之日。"
		if(4)
			day = "图勒之日。"
		if(5)
			day = "芙蕾雅之日。"
		if(6)
			day = "萨图恩之日。"
		if(7)
			day = "太阳之日。"
	. += "噢，不妙，现在是[day]的[station_time_timestamp("hh:mm")]。"
//		testing("mode is [SSshuttle.emergency.mode] should be [SHUTTLE_DOCKED]")
//		if(SSshuttle.emergency.mode == SHUTTLE_DOCKED)
//			if(SSshuttle.emergency.timeLeft() < 30 MINUTES)
//				. += span_warning("The last boat will leave in [round(SSshuttle.emergency.timeLeft()/600)] minutes.")

/obj/structure/fluff/wallclock/Initialize(mapload)
	soundloop = new(src, FALSE)
	soundloop.start()
	. = ..()

/obj/structure/fluff/wallclock/obj_break(damage_flag)
	icon_state = "b[initial(icon_state)]"
	if(soundloop)
		soundloop.stop()
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	..()

/obj/structure/fluff/wallclock/l
	pixel_y = 0
	pixel_x = -32
/obj/structure/fluff/wallclock/r
	pixel_y = 0
	pixel_x = 32
//vampire
/obj/structure/fluff/wallclock/vampire
	name = "古钟"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "wallclockvampire"
	density = FALSE
	anchored = TRUE
	layer = BELOW_MOB_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	pixel_y = 32

/obj/structure/fluff/wallclock/vampire/l
	pixel_y = 0
	pixel_x = -32
/obj/structure/fluff/wallclock/vampire/r
	pixel_y = 0
	pixel_x = 32

/obj/structure/fluff/signage//these are a bit of a pain
	name = "告示牌"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "shitsign"
	density = TRUE
	anchored = TRUE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 200
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')

/obj/structure/fluff/signage/examine(mob/user)
	. = ..()
	if(!user.is_literate())
		. += "我完全看不懂上面写了什么。"
	else
		. += "上面写着“谷地”。"

/obj/structure/fluff/buysign
	icon_state = "signwrote"
	name = "告示牌"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/buysign/examine(mob/user)
	. = ..()
	if(!user.is_literate())
		. += "我完全看不懂上面写了什么。"
	else
		. += "上面写着“进口货”。"

/obj/structure/fluff/sellsign
	icon_state = "signwrote"
	name = "告示牌"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/sellsign/examine(mob/user)
	. = ..()
	if(!user.is_literate())
		. += "我完全看不懂上面写了什么。"
	else
		. += "我能读懂这块牌子。"

/obj/structure/fluff/customsign
	name = "告示牌"
	desc = ""
	icon_state = "sign"
	var/wrotesign
	max_integrity = 200//these don't need to be so tough
	blade_dulling = DULLING_BASHCHOP
	icon = 'icons/roguetown/misc/structure.dmi'
	pixel_y = 3

/obj/structure/fluff/customsign/examine(mob/user)
	. = ..()
	if(wrotesign)
		if(!user.is_literate())
			. += "我完全看不懂上面写了什么。"
		else
			. += "上面写着“[wrotesign]”。"

/obj/structure/fluff/customsign/arrow
	icon_state = "shitsign"

/obj/structure/fluff/customsign/wrote //For mapped in signs and not player-made signs
	icon_state = "signwrote"

/obj/structure/fluff/customsign/attackby(obj/item/W, mob/user, params)
	if(!user.cmode)
		if(!user.is_literate())
			to_chat(user, span_warning("我不会写字。"))
			return
		var/can_write = FALSE
		if((user.used_intent.blade_class == BCLASS_STAB) && (W.wlength == WLENGTH_SHORT))
			can_write = TRUE
		if(istype(W, /obj/item/natural/thorn))
			can_write = TRUE
		if(istype(W, /obj/item/natural/feather))
			can_write = TRUE
		if(istype(W, /obj/item/rogueore/coal))
			can_write = TRUE

		if(can_write)
			if(wrotesign)
				to_chat(user, span_warning("这里已经刻过东西了。"))
				return
			else
				var/inputty = stripped_input(user, "你想在这里刻下什么？", "", null, 200)
				if(inputty && !wrotesign)
					wrotesign = inputty
					icon_state = "signwrote"
		else
			to_chat(user, span_warning("唉，这样可不行。若要刻字，我得用带短小尖头的东西去划。像小刀、荆刺、羽毛，甚至木炭都行。"))
			return
	..()

/obj/structure/fluff/alch
	name = "炼金台"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "alch"
	density = TRUE
	anchored = TRUE
	layer = BELOW_OBJ_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 450
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")

/obj/structure/fluff/alch/folding
	name = "折叠炼金实验台"
	desc = "一座紧凑的实验台，已经铺展开来，随时可以开工。"
	icon = 'icons/roguetown/misc/gadgets.dmi'
	icon_state = "foldingAlchstationDeployed"
	max_integrity = 350
	debris = list(/obj/item/grown/log/tree/small = 2)
	climbable = TRUE
	climb_offset = 10

/obj/structure/fluff/alch/folding/examine()
	. = ..()
	. += span_blue("右键即可将实验台折叠起来。")

/obj/structure/fluff/alch/folding/attack_right(mob/user)
	if(do_after(user, 5 SECONDS, target = src))
		user.visible_message(span_notice("[user]折叠起了[src]。"), span_notice("我将[src]折叠了起来。"))
		new /obj/item/folding_table_stored/alchstation(drop_location())
		qdel(src)
		return ..()

/obj/structure/fluff/statue
	name = "雕像"
	desc = ""
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	icon_state = "bstatue"
	density = FALSE
	anchored = TRUE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASH
	max_integrity = 300
	dir = SOUTH

/obj/structure/fluff/statue/Initialize(mapload)
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/statue/OnCrafted(dirin, user)
	dirin = turn(dirin, 180)
	. = ..()

/obj/structure/fluff/statue/attack_right(mob/user)
	handle_special_items_retrieval(user, src)


/obj/structure/fluff/statue/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, mover) == dir)
		return 0
	return !density

/obj/structure/fluff/statue/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/statue/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(get_dir(leaving.loc, new_location) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/statue/gargoyle
	icon_state = "gargoyle"

/obj/structure/fluff/statue/aasimar
	icon_state = "aasimar"

/obj/structure/fluff/statue/gargoyle/candles
	icon_state = "gargoyle_candles"

/obj/structure/fluff/statue/gargoyle/moss
	icon_state = "mgargoyle"

/obj/structure/fluff/statue/gargoyle/moss/candles
	icon_state = "mgargoyle_candles"

/obj/structure/fluff/statue/knight
	icon_state = "knightstatue_l"

/obj/structure/fluff/statue/astrata
	name = "阿丝塔塔雕像"
	desc = "太阳女神阿丝塔塔的石雕像。愿她赐福。"
	icon_state = "astrata"
	icon = 'icons/roguetown/misc/tallandwide.dmi'

/obj/structure/fluff/statue/astrata/gold
	name = "装饰华美的阿丝塔塔雕像"
	desc = "一尊太阳女神阿丝塔塔的装饰石像，点缀着黄金饰品。愿她赐福。"
	icon_state = "astrata_bling"

//Why are all of these in one giant file.
/obj/structure/fluff/statue/abyssor
	name = "阿比索尔雕像"
	desc = "一尊远古神祇阿比索尔的板岩雕像。毫无疑问，这只是从梦境中描摹出的众多形象之一，而这一尊尤其令人不寒而栗。"
	icon_state = "abyssor"
	icon = 'icons/roguetown/misc/tallandwide.dmi'
	pixel_x = -16

/obj/structure/fluff/statue/abyssor/dolomite
	name = "阿比索尔雕像"
	desc = "一尊罕见的白云岩阿比索尔雕像，由褪色苍白的岩石凿成，仿佛那点微光就能让他无面的凝视显得不那么可怖似的。"
	icon_state = "abyssor_dolomite"

/obj/structure/fluff/statue/knight/r
	icon_state = "knightstatue_r"

/obj/structure/fluff/statue/knight/interior
	icon_state = "oknightstatue_l"

/obj/structure/fluff/statue/knight/interior/r
	icon_state = "oknightstatue_r"

/obj/structure/fluff/statue/knight/interior/r/bronze
	color = "#ff9c1a"

/obj/structure/fluff/statue/knightalt
	icon_state = "knightstatue2_l"

/obj/structure/fluff/statue/knightalt/r
	icon_state = "knightstatue2_r"


/obj/structure/fluff/statue/myth
	icon_state = "myth"
	density = TRUE

/obj/structure/fluff/statue/psy
	icon_state = "psy"
	icon = 'icons/roguetown/misc/96x96.dmi'
	pixel_x = -32

/obj/structure/fluff/statue/psybloody
	icon_state = "psy_bloody"
	icon = 'icons/roguetown/misc/96x96.dmi'
	pixel_x = -32


/obj/structure/fluff/statue/small
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "elfs"

/obj/structure/fluff/statue/pillar
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "pillar"

/obj/structure/fluff/statue/femalestatue
	icon = 'icons/roguetown/misc/ay.dmi'
	icon_state = "1"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue1
	icon = 'icons/roguetown/misc/ay.dmi'
	icon_state = "2"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue2
	icon = 'icons/roguetown/misc/ay.dmi'
	icon_state = "5"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue/zizo
	icon = 'icons/roguetown/misc/ay.dmi'
	icon_state = "4"
	pixel_x = -32
	// pixel_y = -16

/obj/structure/fluff/statue/scare
	name = "稻草人"
	icon_state = "td"

/obj/structure/fluff/statue/tdummy
	name = "练习假人"
	icon_state = "p_dummy"
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/statue/tdummy/attackby(obj/item/W, mob/user, params)
	if(!user.cmode)
		if(W.associated_skill)
			if(user.mind)
				if(isliving(user))
					if(user.doing)
						return
					var/mob/living/L = user
					user.visible_message(span_notice("[user]开始在[src]上训练……"))
					while(do_after(user, 1 SECONDS, target = src))
						if(!(L.mobility_flags & MOBILITY_STAND))
							to_chat(user, span_warning("我被撞倒了，训练也随之中断。"))
							break
						var/probby = (L.STALUC / 10) * 100
						probby = min(probby, 99)
						user.changeNext_move(CLICK_CD_MELEE)
						if(W.max_blade_int)
							W.remove_bintegrity(5)
						L.stamina_add(rand(4,6))
						if(L.STAINT < 3)
							probby = 0
						if(!can_train_combat_skill(user, W.associated_skill, SKILL_LEVEL_APPRENTICE))
							to_chat(user, span_warning("我已经从这种练习里学不到更多了，该去面对真正的实战了。"))
							break
						if(prob(probby) && !user.buckled)
							user.visible_message(span_info("[user]对着[src]展开了训练！"))
							var/amt2raise = L.STAINT * 0.35
							if(amt2raise > 0)
								user.mind.add_sleep_experience(W.associated_skill, amt2raise, FALSE)
							playsound(loc,pick('sound/combat/hits/onwood/education1.ogg','sound/combat/hits/onwood/education2.ogg','sound/combat/hits/onwood/education3.ogg'), rand(50,100), FALSE)
						else
							user.visible_message(span_danger("[user]正在拿[src]训练，可[src]竟然反击了！"))
							L.AdjustKnockdown(1)
							L.throw_at(get_step(L, get_dir(src,L)), 2, 2, L, spin = FALSE)
							playsound(loc, 'sound/combat/hits/kick/stomp.ogg', 100, TRUE, -1)
						flick(pick("p_dummy_smashed","p_dummy_smashedalt"),src)
					return
	..()

/obj/structure/fluff/statue/spider
	name = "母神"
	icon_state = "spidercore"

/obj/structure/fluff/statue/spider/attackby(obj/item/W, mob/user, params)
	if(istype(W, /obj/item/reagent_containers/food/snacks/rogue/honey/spider))
		if(user.mind)
			if(user.mind.special_role == "Dark Elf")
				playsound(loc,'sound/misc/eat.ogg', rand(30,60), TRUE)
				SSmapping.retainer.delf_contribute += 1
				if(SSmapping.retainer.delf_contribute >= SSmapping.retainer.delf_goal)
					say("你做得很好，我的孩子。",language = /datum/language/elvish)
				else
					say("再为我带来[SSmapping.retainer.delf_goal - SSmapping.retainer.delf_contribute]份。我仍在饥饿。",language = /datum/language/elvish)
				qdel(W)
				return TRUE
	..()

/obj/structure/fluff/statue/evil
	name = "神像"
	desc = "一尊献给千面玛修斯的偶像。尽管无人会否认他对暴君秩序与贵族阶层的憎恨，\
	但也同样没人真能想象他究竟长成什么模样。 \
	这不过是那位千面之神诸多形象中的一种，而它看起来依旧随时准备接受祭献。"
	icon_state = "evilidol"
	icon = 'icons/roguetown/misc/structure.dmi'
	damage_deflection = INFINITY//We don't want this smashed normally. Prevents items from doing it damage, by mistake, too.
// What items the idol will accept
	var/treasuretypes = list(
		/obj/item/roguecoin,
		/obj/item/roguegem,
		/obj/item/clothing/ring,
		/obj/item/ingot/gold,
		/obj/item/ingot/silver,
		/obj/item/ingot/blacksteel,
		/obj/item/clothing/neck/roguetown/psicross,
		/obj/item/reagent_containers/glass/cup,
		/obj/item/candle/gold,
		/obj/item/candle/silver,
		/obj/item/candle/candlestick/silver,
		/obj/item/candle/candlestick/gold,
		/obj/item/kitchen/fork/silver,
		/obj/item/kitchen/fork/gold,
		/obj/item/kitchen/spoon/silver,
		/obj/item/kitchen/spoon/gold,
		/obj/item/roguestatue,
		/obj/item/riddleofsteel,
		/obj/item/listenstone,
		/obj/item/clothing/neck/roguetown/shalal,
		/obj/item/clothing/neck/roguetown/horus,
		/obj/item/rogue/painting,
		/obj/item/clothing/head/roguetown/crown/serpcrown,
		/obj/item/clothing/head/roguetown/vampire,
		/obj/item/scomstone,
		/obj/item/reagent_containers/lux,
		/obj/item/cooking/platter/silver,
		/obj/item/cooking/platter/gold,
		/obj/item/reagent_containers/glass/bowl/silver,
		/obj/item/reagent_containers/glass/bowl/gold,
		/obj/item/kitchen/spoon/gold,
		/obj/item/kitchen/spoon/silver,
		/obj/item/candle/candlestick/gold,
		/obj/item/candle/candlestick/silver,
		/obj/item/rogueweapon/sword/long/judgement, // various unique weapons around from a few roles follows. Don't lose your fancy toys....
		/obj/item/rogueweapon/sword/long/oathkeeper,
		/obj/item/rogueweapon/woodstaff/riddle_of_steel/magos, //bit dumb for a bandit mage to toss this toy away but whatever
		/obj/item/clothing/head/roguetown/circlet,
		/obj/item/carvedgem,  //Some of these aren't particularly worth much, but it'd be REALLY unintuitive for "valuables" to not actually be offerings
		/obj/item/rogueweapon/huntingknife/stoneknife/kukri,
		/obj/item/rogueweapon/huntingknife/stoneknife/opalknife,
		/obj/item/rogueweapon/mace/cudgel/shellrungu,
		/obj/item/clothing/mask/rogue/facemask/carved,
		/obj/item/clothing/neck/roguetown/carved,
		/obj/item/kitchen/fork/carved,
		/obj/item/kitchen/spoon/carved,
		/obj/item/clothing/wrists/roguetown/gem,
		/obj/item/reagent_containers/glass/bowl/carved,
		/obj/item/reagent_containers/glass/bucket/pot/carved,
		/obj/item/clothing/mask/rogue/facemask/carved,
		/obj/item/cooking/platter/carved
	)

/obj/structure/fluff/statue/evil/attackby(obj/item/W, mob/user, params)
	if(!HAS_TRAIT(user, TRAIT_COMMIE))
		return
	var/donatedamnt = W.get_real_price()
	if(user.mind)
		if(user)
			if(W.flags_1 & HOARDMASTER_SPAWNED_1)
				to_chat(user, span_warning("这件物品来自宝藏堆！"))
				return
			if(W.sellprice <= 0)
				to_chat(user, span_warning("这件物品一文不值。"))
				return
			var/proceed_with_offer = FALSE
			for(var/TT in treasuretypes)
				if(istype(W, TT))
					proceed_with_offer = TRUE
					break
			if(proceed_with_offer)
				playsound(loc,'sound/items/carvty.ogg', 50, TRUE)
				qdel(W)
				for(var/mob/player in GLOB.player_list)
					if(player.mind)
						if(player.mind.has_antag_datum(/datum/antagonist/bandit))
							var/datum/antagonist/bandit/bandit_players = player.mind.has_antag_datum(/datum/antagonist/bandit)
							record_round_statistic(STATS_SHRINE_VALUE, W.get_real_price())
							bandit_players.favor += donatedamnt
							bandit_players.totaldonated += donatedamnt
							to_chat(player, ("<font color='yellow'>[user.name]向神龛捐献了[donatedamnt]！你现在拥有[bandit_players.favor]点恩惠。</font>"))

			else
				to_chat(user, span_warning("这件物品并不是合适的供品。"))
				return
	..()

/obj/structure/fluff/psycross
	name = "万神殿十字架"
	icon_state = "psycross"
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	break_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	density = FALSE
	anchored = TRUE
	blade_dulling = DULLING_BASHCHOP
	layer = BELOW_MOB_LAYER
	max_integrity = 100
	var/chance2hear = 30
	buckleverb = "钉上十字架"
	can_buckle = 1
	buckle_lying = 0
	breakoutextra = 10 MINUTES
	dir = NORTH
	buckle_requires_restraints = 1
	buckle_prevents_pull = 1
	var/divine = TRUE
	obj_flags = UNIQUE_RENAME | CAN_BE_HIT

/obj/structure/fluff/psycross/Initialize(mapload)
	. = ..()
	become_hearing_sensitive()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/psycross/Destroy()
	lose_hearing_sensitivity()
	return ..()

/obj/structure/fluff/psycross/post_buckle_mob(mob/living/M)
	..()
	M.set_mob_offsets("bed_buckle", _x = 0, _y = 2)
	M.setDir(SOUTH)

/obj/structure/fluff/psycross/post_unbuckle_mob(mob/living/M)
	..()
	M.reset_offsets("bed_buckle")

/obj/structure/fluff/psycross/CanPass(atom/movable/mover, turf/target)
	if(istype(mover, /mob/camera))
		return TRUE
	if(get_dir(loc, mover) == dir)
		return FALSE
	return !density

/obj/structure/fluff/psycross/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/psycross/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(get_dir(leaving.loc, new_location) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/psycross/copper
	name = "万神殿十字架"
	icon_state = "psycrosschurch"
	break_sound = null
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")
	chance2hear = 66

/obj/structure/fluff/psycross/crafted
	name = "木制万神殿十字架"
	icon_state = "psycrosscrafted"
	max_integrity = 80
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix
	name = "木制普赛顿苦像"
	desc = "一种罕见的、象征绝对与虔诚确信的圣徽，在奥塔瓦更为常见：祂仍然活着。祂仍在呼吸。"
	icon_state = "psycruci"
	max_integrity = 80
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix/stone
	name = "石制普赛顿苦像"
	desc = "这座由石料塑成的巨大普赛十字象征着祂永恒长存。在谷地之中，这仍属罕见景象。"
	icon_state = "psycruci_r"
	max_integrity = 120
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix/silver
	name = "银制普赛顿苦像"
	icon_state = "psycruci_s"
	desc = "这尊苦像由受祝圣的白银打造，象征着对唯一真主的绝对信仰。因为普赛顿为一切凡世众生而悲泣。普赛顿为一切行于尘土之上的人而悲泣。普赛顿仍在悲泣……"
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")
	max_integrity = 450
	chance2hear = 10

/obj/structure/fluff/psycross/zizocross
	name = "倒十字"
	desc = "一件不洁圣徽。对大多数人而言是亵渎，对少数人而言却是崇敬。"
	icon_state = "invertedcross"
	divine = FALSE

/obj/structure/fluff/psycross/zizocross/stone
	name = "石制倒十字"
	desc = "一件不洁圣徽。如此坚固的东西竟能在无人阻拦下被树起，只为崇奉那颗暗星……这件事对许多人而言都难以接受。"
	icon_state = "cross_zizo"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/zizocross/golden
	name = "金饰倒十字"
	desc = "一件以金箔细致包覆的不洁圣徽。它公然向秩序挑衅。亡者终将再起。"
	icon_state = "cross_zizo_u"
	divine = FALSE
	max_integrity = 350
	
/obj/structure/fluff/psycross/graggar
	name = "凶戾十字"
	desc = "一件以石料凿成的不洁圣徽。它许诺征服者荣耀，也许诺被征服者镣铐。"
	icon_state = "cross_graggar"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/graggar/decorated
	name = "受崇凶戾十字"
	desc = "一件以石料凿成的不洁圣徽。肉块被串在尖刺上，皮肉像缎带般垂挂于钩间，那是供品，也是征服的证明，可祂真的会聆听吗？"
	icon_state = "cross_graggar_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/matthios
	name = "狞笑十字"
	desc = "一尊不洁石十字，上面刻着出鞘匕首的纹样，以及一张咧嘴狞笑的面容。"
	icon_state = "cross_matthios"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/matthios/decorated
	name = "华饰十字"
	desc = "金色天平悬在破布之间，彼此维持着平衡。这是一座献给财富的纪念碑。"
	icon_state = "cross_matthios_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/baotha
	name = "蛛形十字"
	desc = "一尊扭曲嶙峋的石十字，其上雕出的蛛腿向外舒展。你隐约觉得自己正被它召唤，仿佛耳畔有低语掠过。"
	icon_state = "cross_baotha"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/baotha/decorated
	name = "覆网蛛形十字"
	desc = "蜘蛛张开腿肢，蛛网也随之铺展。只要看着它，糟糕的回忆就会浮上心头。"
	icon_state = "cross_baotha_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/attackby(obj/item/W, mob/living/carbon/human/user, params)
	if(user.mind)
		if((user.mind.assigned_role == "Bishop") || (user.mind.assigned_role == "Acolyte"))
			if(istype(W, /obj/item/reagent_containers/food/snacks/grown/apple))
				if(!istype(get_area(user), /area/rogue/indoors/town/church/chapel))
					to_chat(user, span_warning("我得在礼拜堂里进行这件事。"))
					return FALSE
				var/marriage = FALSE
				var/obj/item/reagent_containers/food/snacks/grown/apple/A = W
				if(A.bitten_names.len == 2)
					var/mob/living/carbon/human/thegroom
					var/mob/living/carbon/human/thebride
					// Find people by bite order, not random viewer order
					for(var/bite_name in A.bitten_names)
						var/found = FALSE
						for(var/mob/M in viewers(src, 7))
							if(!ishuman(M)) continue
							var/mob/living/carbon/human/C = M
							if(C.stat == DEAD) continue
							if(!C.client) continue
							if(C.marriedto) continue
							if(C.real_name == bite_name)
								if(!thegroom)
									thegroom = C  // First bite = groom
								else if(!thebride)
									thebride = C  // Second bite = bride
								found = TRUE
								break
						if(found && thegroom && thebride)
							break
					if(thegroom && thebride)
						// Excommunication check for both participants
						var/excomm_found = FALSE
						for(var/excomm_name in GLOB.excommunicated_players)
							var/clean_excomm = LOWER_TEXT(trim(excomm_name))
							if(thegroom && clean_excomm == LOWER_TEXT(trim(thegroom.real_name)))
								excomm_found = TRUE
								break
							if(thebride && clean_excomm == LOWER_TEXT(trim(thebride.real_name)))
								excomm_found = TRUE
								break
						if(!excomm_found)
							// Prompt priest for surname
							var/surname = input(user, "为这对新人输入一个姓氏：", "婚礼仪式") as text|null
							if(!surname || !length(trim(surname)))
								surname = thegroom.dna.species.random_surname()
							priority_announce("[thegroom.real_name]与[thebride.real_name]结为夫妻！", title = "Holy Union!", sound = 'sound/misc/bell.ogg')
							var/list/titles = list("Sir", "Ser", "Dame", "Lord", "Lady", "Knight-Captain", "Duke", "Duchess", "Father", "Mother", "Brother", "Sister", "Prelate", "Devotee", "Votary")
							// Assign surname to groom
							var/list/groom_name_parts = splittext(thegroom.real_name, " ")
							var/title_found = (titles.Find(groom_name_parts[1]) != 0)
							if(title_found)
								thegroom.real_name = "[groom_name_parts[1]] [groom_name_parts[2]] [surname]"
							else
								thegroom.real_name = "[groom_name_parts[1]] [surname]"
							// Assign surname to bride
							var/list/bride_name_parts = splittext(thebride.real_name, " ")
							title_found = (titles.Find(bride_name_parts[1]) != 0)
							if(title_found)
								thebride.real_name = "[bride_name_parts[1]] [bride_name_parts[2]] [surname]"
							else
								thebride.real_name = "[bride_name_parts[1]] [surname]"
							// Private notification to both
							if(thegroom) 
								to_chat(thegroom, span_notice("你们今后共同的新姓氏是[surname]。"))
							if(thebride) 
								to_chat(thebride, span_notice("你们今后共同的新姓氏是[surname]。"))
							// Set marriedto fields
							thegroom.marriedto = thebride.real_name
							thebride.marriedto = thegroom.real_name
							thegroom.adjust_triumphs(1)
							thebride.adjust_triumphs(1)
							// After surname is set, have the priest say the wedding line
							if(user && surname)
								user.say("我在此宣布，你们结为[surname]家之夫妻。")
							qdel(A)
							marriage = TRUE
						else
							A.become_rotten()
							to_chat(user, span_danger("艾欧拉拒斥这场结合！苹果在你掌中腐烂。被逐出教门之人不得由教会主持婚礼。"))
							if(thegroom)
								to_chat(thegroom, span_danger("艾欧拉拒斥这场结合！你已被逐出教门，无法由教会主持婚礼。"))
							if(thebride)
								to_chat(thebride, span_danger("艾欧拉拒斥这场结合！你已被逐出教门，无法由教会主持婚礼。"))
							// Do not qdel(A) here so the rotten apple remains
							return
					if(!marriage)
						if(istype(W, /obj/item/reagent_containers/food/snacks/grown/apple))
							W.burn()
						return
				return
	..()


/obj/structure/fluff/psycross/copper/Destroy()
	addomen("psycross")
	..()

/obj/structure/fluff/psycross/proc/AOE_flash(mob/user, range = 15, power = 5, targeted = FALSE)
	var/list/mob/targets = get_flash_targets(get_turf(src), range, FALSE)
	for(var/mob/living/carbon/C in targets)
		flash_carbon(C, user, power, targeted, TRUE)
	return TRUE

/obj/structure/fluff/psycross/proc/get_flash_targets(atom/target_loc, range = 15)
	if(!target_loc)
		target_loc = loc
	if(isturf(target_loc) || (ismob(target_loc) && isturf(target_loc.loc)))
		return viewers(range, get_turf(target_loc))
	else
		return typecache_filter_list(target_loc.GetAllContents(), GLOB.typecache_living)

/obj/structure/fluff/psycross/proc/flash_carbon(mob/living/carbon/M, mob/user, power = 15, targeted = TRUE, generic_message = FALSE)
	if(!istype(M))
		return
	if(user)
		log_combat(user, M, "[targeted? "flashed(targeted)" : "flashed(AOE)"]", src)
	else //caused by emp/remote signal
		M.log_message("was [targeted? "flashed(targeted)" : "flashed(AOE)"]",LOG_ATTACK)
	if(generic_message && M != user)
		to_chat(M, span_danger("[src]迸发出刺目的强光！"))
	if(M.flash_act())
		var/diff = power - M.confused
		M.confused += min(power, diff)

/obj/structure/fluff/beach_umbrella/security
	icon_state = "hos_brella"

/obj/structure/fluff/beach_umbrella/science
	icon_state = "rd_brella"

/obj/structure/fluff/beach_umbrella/engine
	icon_state = "ce_brella"

/obj/structure/fluff/beach_umbrella/cap
	icon_state = "cap_brella"

/obj/structure/fluff/beach_umbrella/syndi
	icon_state = "syndi_brella"

/obj/structure/fluff/clockwork
	name = "发条遗物"
	icon = 'icons/obj/clockwork_objects.dmi'
	deconstructible = FALSE

/obj/structure/fluff/clockwork/alloy_shards
	name = "复制合金碎片"
	desc = ""
	icon_state = "alloy_shards"

/obj/structure/fluff/clockwork/alloy_shards/small
	icon_state = "shard_small1"

/obj/structure/fluff/clockwork/alloy_shards/medium
	icon_state = "shard_medium1"

/obj/structure/fluff/clockwork/alloy_shards/medium_gearbit
	icon_state = "gear_bit1"

/obj/structure/fluff/clockwork/alloy_shards/large
	icon_state = "shard_large1"

/obj/structure/fluff/clockwork/blind_eye
	name = "盲眼"
	desc = ""
	icon_state = "blind_eye"

/obj/structure/fluff/clockwork/fallen_armor
	name = "倒下的甲胄"
	desc = ""
	icon_state = "fallen_armor"

/obj/structure/fluff/clockwork/clockgolem_remains
	name = "发条魔像残骸"
	desc = ""
	icon_state = "clockgolem_dead"

/obj/structure/fluff/headstake
	name = "木桩上的头颅"
	desc = ""
	icon = 'icons/roguetown/items/natural.dmi'
	icon_state = "headstake"
	density = FALSE
	anchored = TRUE
	dir = SOUTH
	var/obj/item/grown/log/tree/stake/stake
	var/obj/item/bodypart/head/victim


/obj/structure/fluff/headstake/CheckParts(list/parts_list)
	..()
	victim = locate(/obj/item/bodypart/head) in parts_list
	name = "木桩上的[victim.name]"
	update_icon()
	stake = locate(/obj/item/grown/log/tree/stake) in parts_list

///obj/structure/fluff/headstake/Initialize()
//	. = ..()

/obj/structure/fluff/headstake/OnCrafted(dirin, user)
	dir = SOUTH
	pixel_x = rand(-8, 8)
	return

/obj/structure/fluff/headstake/update_icon()
	..()
	var/obj/item/bodypart/head/H = locate() in contents
	var/mutable_appearance/MA = new()
	if(H)
		MA.copy_overlays(H)
		H.pixel_y = rand(9, 11)
		H.pixel_x = pixel_x
		H.dir = SOUTH
		add_overlay(H)

/obj/structure/fluff/headstake/attack_hand(mob/user)
	. = ..()
	if(.)
		return
	to_chat(user, span_notice("我拆下了[src]。"))
	victim.forceMove(drop_location())
	victim = null
	stake.forceMove(drop_location())
	stake = null
	qdel(src)

/obj/structure/fluff/statue/noc
	name = "诺克雕像"
	desc = "智慧与沉静。"
	icon_state = "noc"
	icon = 'icons/roguetown/misc/statues/statue_noc.dmi'
	pixel_x = -16

/obj/structure/fluff/statue/noc/guard
	name = "战姿诺克雕像"
	icon_state = "noc_guard"

/obj/structure/fluff/statue/eora
	name = "艾欧拉雕像"
	desc = "美丽与魅力。"
	icon_state = "eora"
	icon = 'icons/roguetown/misc/statues/statue_eora.dmi'
	pixel_x = -16

/obj/structure/fluff/statue/zizo
	name = "可疑雕像"
	desc = "亵渎……除非……？"
	icon_state = "zaelorian_crynsaris"
	icon = 'icons/roguetown/misc/statues/statue_zizo.dmi'
	pixel_x = -16
