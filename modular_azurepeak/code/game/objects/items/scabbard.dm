/obj/item/rogueweapon/scabbard
	name = "剑鞘"
	desc = ""

	icon = 'modular_azurepeak/icons/obj/items/scabbard.dmi'
	lefthand_file = 'icons/mob/inhands/equipment/backpack_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/backpack_righthand.dmi'
	icon_state = "scabbard"
	item_state = "scabbard"

	parrysound = "parrywood"
	attacked_sound = "parrywood"

	anvilrepair = /datum/skill/craft/blacksmithing

	slot_flags = ITEM_SLOT_HIP|ITEM_SLOT_BACK
	possible_item_intents = list(SHIELD_BASH)
	sharpness = IS_BLUNT
	wlength = WLENGTH_SHORT
	resistance_flags = FLAMMABLE
	blade_dulling = DULLING_BASHCHOP
	w_class = WEIGHT_CLASS_BULKY
	alternate_worn_layer = UNDER_CLOAK_LAYER
	experimental_onhip = TRUE
	experimental_onback = TRUE
	can_parry = FALSE

	COOLDOWN_DECLARE(shield_bang)

	/// Weapon path and its children that are allowed
	var/obj/item/rogueweapon/valid_blade
	/// Specific weapons that are allowed. Bypasses valid_blade
	var/list/obj/item/rogueweapon/valid_blades
	/// Specific weapons that are not allowed. Bypassed valid_blade
	var/list/obj/item/rogueweapon/invalid_blades

	/// Stores weapon
	var/obj/item/rogueweapon/sheathed

	var/sheathe_time = 0.1 SECONDS
	var/sheathe_sound = 'sound/foley/equip/scabbard_holster.ogg'


/obj/item/rogueweapon/scabbard/attack_obj(obj/O, mob/living/user)
	return FALSE


/obj/item/rogueweapon/scabbard/attack_turf(turf/T, mob/living/user)
	to_chat(user, span_notice("我开始寻找自己的剑......"))
	for(var/obj/item/rogueweapon/sword/sword in T.contents)
		if(eat_sword(user, sword))
			break

	..()


/obj/item/rogueweapon/scabbard/proc/weapon_check(mob/living/user, obj/A)
	if(sheathed)
		to_chat(user, span_warning("鞘里已经有东西了！"))
		return FALSE
	if(valid_blade && !istype(A, valid_blade))
		to_chat(user, span_warning("[A]塞不进去。"))
		return FALSE
	if(valid_blades)
		if(!(A.type in valid_blades))
			to_chat(user, span_warning("[A]塞不进去。"))
			return FALSE
	if(invalid_blades)
		if(A.type in invalid_blades)
			to_chat(user, span_warning("[A]塞不进去。"))
			return FALSE
	return TRUE


/obj/item/rogueweapon/scabbard/proc/eat_sword(mob/living/user, obj/A, sheathing_from_belt = FALSE)
	if(!weapon_check(user, A))
		return FALSE
	if(obj_broken)
		user.visible_message(
			span_warning("[user] begins to force [A] into [src]!"),
			span_warningbig("我开始把[A]硬塞进[src]。")
		)
		if(!move_after(user, 2 SECONDS, target = user))
			return FALSE
		return FALSE
	if(!move_after(user, sheathe_time, target = user))
		return FALSE

	A.forceMove(src)
	sheathed = A
	update_icon(user)

	if(!sheathing_from_belt)
		user.visible_message(
			span_notice("[user]将[A]收入[src]中。"),
			span_notice("我将[A]收入[src]中。")
		)

	playsound(src, sheathe_sound, 100, TRUE)
	return TRUE


/obj/item/rogueweapon/scabbard/proc/puke_sword(mob/living/user)
	if(!sheathed)
		return FALSE

	if(obj_broken)
		user.visible_message(
			span_warning("[user] begins to force [sheathed] out of [src]!"),
			span_warningbig("我开始把[sheathed]硬从[src]里拽出来。")
		)
		if(!move_after(user, 2 SECONDS, target = user))
			return FALSE
	if(!move_after(user, sheathe_time, target = user))
		return FALSE

	sheathed.forceMove(user.loc)
	sheathed.pickup(user)
	user.put_in_hands(sheathed)
	sheathed = null
	update_icon(user)

	user.visible_message(
		span_warning("[user]从[src]中拔出了武器！"),
		span_notice("我从[src]中拔出了武器。")
	)
	return TRUE


/obj/item/rogueweapon/scabbard/MouseDrop(atom/over)
	..()
	var/mob/living/M = usr

	if(!Adjacent(M))
		return
	if(!M.incapacitated() && istype(over, /atom/movable/screen/inventory/hand))
		var/atom/movable/screen/inventory/hand/H = over
		if(M.putItemFromInventoryInHandIfPossible(src, H.held_index))
			add_fingerprint(usr)


/obj/item/rogueweapon/scabbard/attack_hand(mob/user)
	if(sheathed)
		return puke_sword(user)

	..()


/obj/item/rogueweapon/scabbard/attackby(obj/item/I, mob/user, params)
	if(!sheathed)
		if(!eat_sword(user, I))
			return ..()


/obj/item/rogueweapon/scabbard/examine(mob/user)
	. = ..()

	if(sheathed)
		. += span_notice("鞘里装着[sheathed]。左键即可将它拔出。")


/obj/item/rogueweapon/scabbard/update_icon(mob/living/user)
	if(sheathed)
		icon_state = "[initial(icon_state)]_[sheathed.sheathe_icon]"
	else
		icon_state = "[initial(icon_state)]"

	if(user)
		user.update_inv_hands()
		user.update_inv_belt()
		user.update_inv_back()

	getonmobprop(tag)


/obj/item/rogueweapon/scabbard/getonmobprop(tag)
	..()

	if(tag)
		switch(tag)
			if("gen")
				return list(
					"shrink" = 0.6,
					"sx" = -6,
					"sy" = -1,
					"nx" = 6,
					"ny" = -1,
					"wx" = 0,
					"wy" = -2,
					"ex" = 0,
					"ey" = -2,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 1,
					"sflip" = 0,
					"wflip" = 1,
					"eflip" = 0
				)
			if("onback")
				return list(
					"shrink" = 0.5,
					"sx" = 1,
					"sy" = 4,
					"nx" = 1,
					"ny" = 2,
					"wx" = 3,
					"wy" = 3,
					"ex" = 0,
					"ey" = 2,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 8,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 0,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0
				)
			if("onbelt")
				return list(
					"shrink" = 0.5,
					"sx" = -2,
					"sy" = -5,
					"nx" = 4,
					"ny" = -5,
					"wx" = 0,
					"wy" = -5,
					"ex" = 2,
					"ey" = -5,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = -90,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 0,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 1
				)


/*
	DAGGER SHEATHS
*/


/obj/item/rogueweapon/scabbard/sheath
	name = "匕首套"
	desc = "一个可悬挂的皮制刀套，专门收纳那些更小巧的惊喜。"
	sewrepair = TRUE

	icon_state = "sheath"
	item_state = "sheath"

	valid_blade = /obj/item/rogueweapon/huntingknife
	w_class = WEIGHT_CLASS_SMALL

	grid_width = 32
	grid_height = 64
	dropshrink = 0.85

	force = 3
	max_integrity = 500
	sellprice = 2

	invalid_blades = list(
		/obj/item/rogueweapon/huntingknife/idagger/silver/stake
	)

/obj/item/rogueweapon/scabbard/sheath/weapon_check(mob/living/user, obj/item/A)
	. = ..()
	if(.)
		if(!istype(A, /obj/item/rogueweapon))
			return
		var/obj/item/rogueweapon/sheathing = A
		if(!sheathing.sheathe_icon)
			return FALSE

/obj/item/rogueweapon/scabbard/sheath/getonmobprop(tag)
	..()

	if(tag)
		switch(tag)
			if("gen")
				return list(
					"shrink" = 0.5,
					"sx" = -6,
					"sy" = -1,
					"nx" = 6,
					"ny" = -1,
					"wx" = 0,
					"wy" = -2,
					"ex" = 0,
					"ey" = -2,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 1,
					"sflip" = 1,
					"wflip" = 1,
					"eflip" = 0
				)
			if("onback")
				return list(
					"shrink" = 0.4,
					"sx" = -3,
					"sy" = -1,
					"nx" = 0,
					"ny" = 0,
					"wx" = -4,
					"wy" = 0,
					"ex" = 2,
					"ey" = 1,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 0,
					"sturn" = 10,
					"wturn" = 32,
					"eturn" = -23,
					"nflip" = 0,
					"sflip" = 8,
					"wflip" = 8,
					"eflip" = 0
				)
			if("onbelt")
				return list(
					"shrink" = 0.5,
					"sx" = -2,
					"sy" = -5,
					"nx" = 4,
					"ny" = -5,
					"wx" = 0,
					"wy" = -5,
					"ex" = 2,
					"ey" = -5,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 0,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 1
				)


/*
	GREATWEAPON STRAPS
*/


/obj/item/rogueweapon/scabbard/gwstrap
	name = "巨型武器背带"
	desc = ""

	icon_state = "gws0"
	item_state = "gwstrap"
	icon = 'modular_azurepeak/icons/obj/items/gwstrap.dmi'
	lefthand_file = 'icons/mob/inhands/equipment/backpack_lefthand.dmi'
	righthand_file = 'icons/mob/inhands/equipment/backpack_righthand.dmi'
	pixel_y = -16
	pixel_x = -16
	inhand_x_dimension = 64
	inhand_y_dimension = 64

	w_class = WEIGHT_CLASS_BULKY
	slot_flags = ITEM_SLOT_BACK
	resistance_flags = NONE
	experimental_onback = FALSE
	bigboy = TRUE

	equip_delay_self = 5 SECONDS
	unequip_delay_self = 5 SECONDS
	strip_delay = 2 SECONDS
	sheathe_time = 2 SECONDS

	max_integrity = 0
	sellprice = 15

/obj/item/rogueweapon/scabbard/gwstrap/weapon_check(mob/living/user, obj/item/A)
	. = ..()
	if(.)
		if(sheathed)
			return FALSE
		if(istype(A, /obj/item/rogueweapon))
			if(A.w_class >= WEIGHT_CLASS_BULKY)
				return TRUE
		if(!istype(A, /obj/item/clothing/neck/roguetown/psicross)) //snowflake that bypasses the valid_blades that i made. i will commit seppuku eventually
			return FALSE

/obj/item/rogueweapon/scabbard/gwstrap/update_icon(mob/living/user)
	if(sheathed)
		worn_x_dimension = 64
		worn_y_dimension = 64
		icon = sheathed.icon
		icon_state = sheathed.icon_state
		experimental_onback = TRUE
	else
		icon = initial(icon)
		icon_state = initial(icon_state)
		worn_x_dimension = initial(worn_x_dimension)
		worn_y_dimension = initial(worn_y_dimension)
		experimental_onback = FALSE

	if(user)
		user.update_inv_back()

	getonmobprop(tag)

/obj/item/rogueweapon/scabbard/gwstrap/getonmobprop(tag)
	..()
	if(!sheathed)
		return
	if(istype(sheathed, /obj/item/rogueweapon/estoc) || istype(sheathed, /obj/item/rogueweapon/greatsword))
		switch(tag)
			if("onback")
				return list(
					"shrink" = 0.6,
					"sx" = -1,
					"sy" = 2,
					"nx" = 0,
					"ny" = 2,
					"wx" = 2,
					"wy" = 1,
					"ex" = 0,
					"ey" = 1,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 70,
					"eturn" = 15,
					"nflip" = 1,
					"sflip" = 1,
					"wflip" = 1,
					"eflip" = 1,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0
				)
	else
		switch(tag)
			if("onback")
				return list(
					"shrink" = 0.7,
					"sx" = 1,
					"sy" = -1,
					"nx" = 1,
					"ny" = -1,
					"wx" = 4,
					"wy" = -1,
					"ex" = -1,
					"ey" = -1,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 8,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 0,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0
				)


/*
	GENERIC SCABBARDS
*/


/obj/item/rogueweapon/scabbard/sword
	name = "简易剑鞘"
	desc = "长剑出现后，自然而然演化出的配套之物。"

	icon_state = "scabbard"
	item_state = "scabbard"

	sewrepair = TRUE

	valid_blade = /obj/item/rogueweapon/sword
//You'd think think it'd look for subtypes, but no.
	invalid_blades = list(
		/obj/item/rogueweapon/sword/long/exe,
		/obj/item/rogueweapon/sword/long/exe/astrata,
		/obj/item/rogueweapon/sword/long/martyr
	)

	force = 7
	max_integrity = 750
	sellprice = 3

/obj/item/rogueweapon/scabbard/sheath/weapon_check(mob/living/user, obj/item/A)
	. = ..()
	if(.)
		if(!istype(A, /obj/item/rogueweapon))
			return
		var/obj/item/rogueweapon/sheathing = A
		if(!sheathing.sheathe_icon)
			return FALSE


/*
	KAZENGUN
*/


/obj/item/rogueweapon/scabbard/sword/kazengun
	name = "简式风军剑鞘"
	desc = "木衬钢鞘。很适合用来格挡来袭打击。"
	icon_state = "kazscab"
	item_state = "kazscab"

	valid_blade = /obj/item/rogueweapon/sword/sabre/mulyeog
	associated_skill = /datum/skill/combat/shields
	possible_item_intents = list(SHIELD_BASH, SHIELD_BLOCK)
	can_parry = TRUE
	wdefense = 8

	max_integrity = 220

/obj/item/rogueweapon/scabbard/sword/kazengun/noparry
	name = "仪式风军剑鞘"
	desc = "一把以青铜修边的简易木鞘。不同于它的钢制同类，这把无法招架。"

	valid_blade = /obj/item/rogueweapon/sword/long/kriegmesser/ssangsudo
	can_parry = FALSE

/obj/item/rogueweapon/scabbard/sword/kazengun/noparry/loadout
	name = "仪式剑鞘"
	desc = "一把以青铜修边的简易木鞘。不同于它的钢制同类，这把无法招架。"
	valid_blade = /obj/item/rogueweapon/sword
	invalid_blades = list(
		/obj/item/rogueweapon/sword/long/exe,
		/obj/item/rogueweapon/sword/long/exe/astrata,
		/obj/item/rogueweapon/sword/long/martyr
	)

/obj/item/rogueweapon/scabbard/sword/kazengun/steel
	name = "云纹剑鞘"
	desc = "一把带有云纹与布绶带的剑鞘。可用于格挡。"
	icon_state = "kazscab_steel"
	item_state = "kazscab_steel"
	valid_blade = /obj/item/rogueweapon/sword/sabre/mulyeog/rumahench


/obj/item/rogueweapon/scabbard/sword/kazengun/gold
	name = "鎏金心意剑鞘"
	desc = "一把带绶带的华丽木鞘。很适合用来招架。"
	icon_state = "kazscab_gold"
	item_state = "kazscab_gold"
	valid_blade = /obj/item/rogueweapon/sword/sabre/mulyeog/rumacaptain
	max_integrity = 220
	sellprice = 50

/obj/item/rogueweapon/scabbard/sword/kazengun/kodachi
	name = "素漆剑鞘"
	desc = "一把朴素上漆的剑鞘，配有简洁钢制配件。以一条深色素布悬挂在腰带上。"
	icon_state = "kazscabyuruku"
	item_state = "kazscabyuruku"
	valid_blade = /obj/item/rogueweapon/sword/short/kazengun
	wdefense = 4

/obj/item/rogueweapon/scabbard/sheath/kazengun
	name = "素漆刀套"
	desc = "一个简洁的上漆刀套，适用于较短的东方风格刀刃。"
	icon_state = "kazscabdagger"
	item_state = "kazscabdagger"
	valid_blade = /obj/item/rogueweapon/huntingknife/idagger/steel/kazengun
	associated_skill = /datum/skill/combat/shields
	possible_item_intents = list(SHIELD_BASH, SHIELD_BLOCK)
	can_parry = TRUE
	wdefense = 3

	max_integrity = 0
